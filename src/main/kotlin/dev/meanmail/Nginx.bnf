{
  parserClass="dev.meanmail.psi.parser.NginxParser"
  parserUtilClass="com.intellij.lang.parser.GeneratedParserUtilBase"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix=""
  psiImplClassSuffix="Impl"
  psiPackage="dev.meanmail.psi"
  psiImplPackage="dev.meanmail.psi.impl"

  elementTypeHolderClass="dev.meanmail.psi.Types"
  elementTypeClass="dev.meanmail.psi.NginxElementType"
  tokenTypeClass="dev.meanmail.psi.NginxTokenType"

  psiImplUtilClass="dev.meanmail.psi.PsiImplUtil"

  implements("directive_stmt")="dev.meanmail.psi.NamedElement"
}

nginxFile ::= statement*

private statement ::= directive_stmt [COMMENT]

string_stmt ::= (QUOTE [STRING] QUOTE) | (DQUOTE [DQSTRING] DQUOTE)
name_stmt ::= IDENTIFIER
              | VALUE
              | string_stmt
value_stmt ::= IDENTIFIER
               | VALUE
               | string_stmt

block_stmt ::= LBRACE statement* RBRACE {pin=1}
directive_stmt ::= name_stmt value_stmt* (SEMICOLON | block_stmt) {pin=1}
